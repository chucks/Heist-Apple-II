GETBYTE EQU $C000
PRIPAGE EQU $C054
SECPAGE EQU $C055
GRAPHIC EQU $C050
HIRES EQU $C057
NOMIX EQU $C052
SPKR EQU $C030
CLRBYTE EQU $C010
JOYREAD EQU $FB1E
SCLO EQU $F9
SCHI EQU $FA
TABHI EQU $FD 
TABLO EQU $EB 
SHLO EQU $FB
SHHI EQU $FC
CONTROLLER EQU $300 
THIEVES EQU $301
GAMESPEED EQU $303
*
 ORG $0C00
*
*******************************
* PROGRAM BEGINS HERE
*******************************
BEGINNING LDA RUNSPEED 
 SEC
 SBC #$30
 STA RUNSPEED
 LDA GAMESPEED
 CMP SPEEDCOUNT
 BEQ STARTUP
 INC SPEEDCOUNT
 JMP BEGINNING 
STARTUP LDA GRAPHIC
 LDA HIRES
 LDA NOMIX
 STA CLRBYTE
 LDA #$1C 
 STA TABHI+1
 STA TABLO+1
 LDA #$C0 
 STA TABLO
 LDA #$00 
 STA TABHI
 STA JUMPPOINTER
 LDA #$7F
 STA LOAD+2
 LDA #$FF
 STA LOAD+1
 JSR PRNTMEN
 LDA #$03 
 STA FLAGBYTE
 LDX #$0C 
 STX DELAYBYTE
DELAY LDA #$01 
 JSR WAIT
 DEC DELAYBYTE
 LDA DELAYBYTE 
 BNE DELAY
START LDA CONTROLLER
 CMP #$01
 BEQ JOYSTICK 
 LDA GETBYTE
 STA CLRBYTE
JOYKEY STA SAVA
 AND #$80
 CMP #$80
 BNE NOKEY 
 LDA PAGE
 CMP #02
 BNE NOGETAWAY2 
 LDA XLOC
 CMP #24
 BNE NOGETAWAY2 
 JMP GETWAY
*
*
NOKEY JMP NOKEY1 
*
*
JOYSTICK LDX #$01 
 JSR JOYREAD 
 STY SAVA 
 BIT SAVA 
 BMI NOTUP
 BVS NOTUP
 LDA #'A' 
 JMP JOYKEY 
NOTUP BPL NOKEY 
 BVC NOKEY 
 LDA #'Z' 
 JMP JOYKEY
NOGETAWAY2 LDA SAVA
 CMP #'A' 
 BNE CHECKDOWN
 LDY JUMPPOINTER
 LDA JUMPTABLE,Y 
 CMP XLOC
 BNE DEATH
 INC JUMPPOINTER
 INY
 LDA JUMPTABLE,Y 
 INC JUMPPOINTER
 CMP #$01
 BNE DEATH7 
 JSR UP
 JMP START
DEATH JMP DEATH3 
DEATH7 DEC JUMPPOINTER
 DEC JUMPPOINTER
 JMP DEATH3
CHECKDOWN CMP #'Z' 
 BNE NOKEY1 
 LDY JUMPPOINTER
 LDA JUMPTABLE,Y
 CMP XLOC
 BNE DEATH
 INC JUMPPOINTER
 INY 
 LDA JUMPTABLE,Y
 INC JUMPPOINTER
 CMP #$00
 BNE DEATH7 
 JSR DOWN
 JMP START
NOKEY1 LDA PAGE
 CMP #$02
 BNE NOGETAWAY
 LDA XLOC
 CMP #$18 
 BNE NOGETAWAY
 JMP GETWAY
NOGETAWAY LDY JUMPPOINTER
 LDA JUMPTABLE,Y
 CMP XLOC
 BEQ DEATH
 LDA #$00
 STA FLAGBYTE
 INC XLOC
 LDA #$03 
 STA SHAPE
 JSR CHARGEN
 LDA RUNSPEED 
 JSR WAIT
 LDA #$04 
 STA SHAPE
 JSR CHARGEN
 LDA RUNSPEED 
 JSR WAIT
 LDA #$00 
 STA SHAPE
 JSR CHARGEN 
 LDA XLOC
 CMP #$27 
 BNE TARG 
PAGEFLIP INC PAGE
 LDA PAGE
 CMP #$01 
 BNE PAGE1
 LDA SECPAGE
 LDA #$00 
 STA LOAD+1
 LDA #$60
 STA LOAD+2
 LDA #$1E 
 STA TABHI+1
 STA TABLO+1
 JSR PRNTMEN
 JMP START
PAGE1 LDA PRIPAGE
 LDA #$1C 
 STA TABHI+1
 STA TABLO+1
 JSR PRNTMEN
TARG JMP START
*
*
PRNTMEN LDA #$00
 STA SHAPE
 LDA YLOC
 STA SAFEY
 LDA #$08 
 STA YLOC
 LDA #$FD
 STA XLOC
 LDA #$04
 STA MENCOUNT
PRNTMEN2 INC XLOC
 INC XLOC
 INC XLOC
 JSR CHARGEN
 DEC MENCOUNT
 BNE PRNTMEN2
 LDA THIEVES
 STA MENCOUNT
 DEC MENCOUNT
 BEQ DONE
 LDA #$00
 STA XLOC
 LDA #$03
 STA SHAPE
PRNTMEN3 JSR CHARGEN
 DEC MENCOUNT
 BEQ DONE
 INC XLOC
 INC XLOC
 INC XLOC
 JMP PRNTMEN3 
DONE LDA #$00
 STA XLOC
 LDA SAFEY
 STA YLOC
 RTS 
****************************
* SUBROUTINE TO JMP CHAR
****************************
DOWN LDA #$00
 STA SHAPE
 LDA #$01
 STA FLAGBYTE
 STA PITCH
 JSR CHARGEN
 LDA #$05
 STA SHAPE
 STA SHAPE2
 LDA #$06 
 STA COUNT
 JSR CHARGEN 
 LDA #$01 
 JSR WAIT 
 LDA #$00
 STA SHAPE
 JSR CHARGEN
 INC XLOC
 INC SHAPE2
 LDA SHAPE2
 STA SHAPE
 JSR CHARGEN
 LDA #$01 
 JSR WAIT
 LDA #00
 STA SHAPE
 JSR CHARGEN
 LDA YLOC
 CLC
 ADC #$06
 STA YLOC
 INC XLOC
***************************
LOOP INC SHAPE2 
 LDA SHAPE2
 STA SHAPE
 JSR CHARGEN
 LDA #$01 
 JSR WAIT
 LDA #$00
 STA SHAPE
 JSR CHARGEN
 LDA YLOC
 CLC
 ADC #$04
 STA YLOC
 INC XLOC
 LDA XLOC
 CMP #$27 
 BEQ PAGEFLIP2 
 DEC COUNT
 BNE LOOP
 DEC XLOC
 RTS
PAGEFLIP2 LDA PAGE
 CMP #$01 
 BEQ PAGEFLIP3
 LDA YLOC 
 CLC
 ADC #$04
 STA YLOC
 JMP PAGEFLIP
PAGEFLIP3 JMP PAGEFLIP 
***************************
UP LDA #$00
 STA SHAPE
 LDA #$02
 STA FLAGBYTE
 LDA #$39 
 STA PITCH
 JSR CHARGEN
 LDA #$05 
 STA SHAPE
 STA SHAPE2
 LDA #$05
 STA COUNT
LOOP2 JSR CHARGEN
 LDA #$01 
 JSR WAIT
 LDA #00
 STA SHAPE
 JSR CHARGEN
 LDA YLOC
 SEC 
 SBC #$03
 STA YLOC
 INC XLOC
 INC SHAPE2 
 LDA SHAPE2
 STA SHAPE
 DEC COUNT
 BNE LOOP2
 LDA YLOC 
 SEC
 SBC #$05 
 STA YLOC
 JSR CHARGEN 
 LDA #$01 
 JSR WAIT
 LDA #00
 STA SHAPE
 JSR CHARGEN
 INC XLOC
 RTS
*******************************
* PROGRAM TO MAKE SOUND AND
* MOVE MEMORY FROM $6000 ON
* TO HI-RES PAGE 1 AT $2000
*******************************
*
*
WAIT STA WAITVAL 
 LDA FLAGBYTE
 CMP #$02
 BEQ PRESET 
 CMP #$01
 BNE NOPRESET
PRESET LDX #$E8
 JMP CHECK
NOPRESET LDX #$00 
CHECK LDA LOAD+1
 CMP #$FF 
 BNE LOAD 
 LDA LOAD+2
 CMP #$7F 
 BNE LOAD 
 LDA #$48  
 JSR PAUSE
 JMP TIMER
LOAD LDA $6000
 TAY
DEST1 LDA $2000
LOAD1 STA $6000
 TYA
DEST STA $2000
 LDA LOAD+1
 CLC
 ADC #$01 
 STA LOAD+1
 STA LOAD1+1
 BCC INCSAVE
 INC LOAD+2
 INC LOAD1+2
INCSAVE LDA DEST+1 
 CLC
 ADC #$01 
 STA DEST+1
 STA DEST1+1
 BCC INCSAVE2 
 INC DEST+2
 INC DEST1+2
INCSAVE2 INX 
 CPX #$FF 
 BNE CHECK
TIMER LDA WAITVAL 
 JSR PAUSE
 JSR INTERP ;SOUND 
 LDY TWINKPOINT
 LDA TWINKLE,Y
 STA SCLO
 LDA TWINKLE+1,Y
 STA SCHI
 LDY #$00
 LDA #$01 
 STA (SCLO),Y
 LDA SCHI
 CLC
 ADC #$20
 STA SCHI
 LDA #$01 
 STA (SCLO),Y
 LDA TWINKPOINT
 CMP #$12 
 BNE NORESET
 LDA #$FE 
 STA TWINKPOINT
NORESET INC TWINKPOINT
 INC TWINKPOINT
 LDY TWINKPOINT
 LDA TWINKLE,Y
 STA SCLO
 LDA TWINKLE+1,Y
 STA SCHI
 LDY #$00
 LDA #$00 
 STA (SCLO),Y
 LDA SCHI
 CLC
 ADC #$20
 STA SCHI
 LDA #$00
 STA (SCLO),Y
 RTS
*************************
* ROUTINE TO DIE
*************************
DEATH3 LDX JUMPPOINTER
 LDA JUMPTABLE,X 
 CLC 
 ADC #$03 
 STA JUMPTABLE,X 
 LDA #$00 
 STA SHAPE
 JSR CHARGEN
 LDA #$05 
 STA SHAPE
 INC XLOC
 JSR CHARGEN
 LDA #$64 
 JSR PAUSE
 LDA #$00 
 STA SHAPE
 JSR CHARGEN
*****************************
LOOPFLY LDA #$0D
 STA SHAPE
 LDA #$04
 STA FLAGBYTE
 JSR CHARGEN
 LDA #$FF 
 JSR WAIT 
 LDA #$00 
 STA SHAPE
 JSR CHARGEN
 DEC XLOC
 LDX JUMPPOINTER
 LDA JUMPTABLE,X 
 CMP XLOC 
 BEQ DEATH4
 INC XLOC
 INC XLOC
 JMP LOOPFLY
*************************
DEATH4 LDA #$00 
 STA SHAPE
 LDA #$05
 STA FLAGBYTE
 LDA #$08 
 STA PITCH
 LDX #$03
 STX DEATHCOUNTER
 JSR CHARGEN
 LDA #$0E 
 STA SHAPE
 JSR CHARGEN
 LDA #$40  
 JSR WAIT
 LDA #$00
 STA SHAPE
 JSR CHARGEN
 LDA #$0F 
 STA DEATHSHAPE 
NOCHECK1 LDA YLOC
 CMP #$AB 
 BCS DEATH5 
 INC YLOC
 INC YLOC
 LDA DEATHSHAPE 
 STA SHAPE
 JSR CHARGEN
 LDA #$40 
 JSR WAIT
 LDA #$00
 STA SHAPE
 JSR CHARGEN
 LDY JUMPPOINTER
 LDA JUMPTABLE,Y
 CMP #$23 
 BNE NOCHECK2 
 LDA YLOC
 CMP #$47 
 BCS DEATH5
NOCHECK2 LDA YLOC
 CMP #$AB 
 BCS DEATH5 
 DEC DEATHCOUNTER
 BNE NOCHECK1
 LDX #$03
 STX DEATHCOUNTER
 LDA DEATHSHAPE
 CMP #$0F
 BEQ DEATHSHAPE2
 LDA #$0F
 STA DEATHSHAPE
 JMP NOCHECK6
DEATHSHAPE2 LDA #$17
 STA DEATHSHAPE
NOCHECK6 LDA YLOC
 CMP #$AB 
 BCS DEATH5
 JMP NOCHECK1 
DEATH5 LDA #$06 
 STA FLAGBYTE
 LDY JUMPPOINTER
 LDA JUMPTABLE,Y
 CMP #$23 
 BNE NOCHECK7
 LDA #$5A 
 STA BOTTOM+1
NOCHECK7 LDA #$18 
 STA SHAPE
 INC YLOC 
 JSR CHARGEN
 LDA #$FF 
 JSR WAIT
 LDA #$00 
 STA SHAPE
 JSR CHARGEN
 INC YLOC 
 LDA #$00 
 STA COUNTER
 STA NOTE
DIELOOP LDA #$18 
 STA SHAPE
 JSR CHARGEN
 JSR DSOUND 
 LDA #$00 
 STA SHAPE
 JSR CHARGEN
 INC YLOC 
 INC COUNTER
 LDA COUNTER
 CMP #$09 
 BNE DIELOOP
 LDA #$06
 STA PLAYON
PLAYLOOP JSR DSOUND
 DEC PLAYON
 BNE PLAYLOOP
 LDA #$FF
 JSR PAUSE
 JMP RESTORE ;RESURECT IT 
DSOUND LDX NOTE
 LDA NOTETABLE,X
 STA PITCH
 LDA NOTETABLE+1,X
 STA DURATION1 
 INC NOTE
 INC NOTE
 JSR TONE
 RTS 
GETWAY LDA #$02 
 STA COUNTER
GETWAY2 LDX #$00 
 STX SHAPE
 STX FLAGBYTE
 JSR CHARGEN
 INC XLOC
 LDX #$03
 STX SHAPE
 JSR CHARGEN
 LDA #$FF
 JSR WAIT 
 LDX #$04
 STX SHAPE 
 JSR CHARGEN
 LDA #$FF
 JSR WAIT 
 DEC COUNTER
 BNE GETWAY2 
 LDX #$00
 STX SHAPE
 JSR CHARGEN
 LDA YLOC
 STA SAFEY
TOCONTROL JSR ERASE
 LDA #$07
 STA FLAGBYTE
 DEC YLOC 
 JSR TAKEOFF 
 LDA FINISH
 CMP #$00
 BEQ END
 DEC FINISH 
 JMP TOCONTROL 
END JMP CLRSCRN   
**************************
ERASE LDX #$00 
 STX SHAPE
 LDX #$1D 
 STX XLOC
 JSR CHARGEN
 LDX #$20 
 STX XLOC
 JSR CHARGEN
 LDX #$1D 
 STX XLOC
 JSR SUBROUT 
 LDX #$20 
 STX XLOC
 JSR SUBROUT 
 RTS
************************
TAKEOFF LDX #$12 
 STX SHAPE
 LDX #$1D 
 STX XLOC
 JSR CHARGEN
 LDX #$13 
 STX SHAPE
 LDX #$20 
 STX XLOC
 JSR CHARGEN
 LDX #$10 
 STX SHAPE
 LDX #$1D 
 STX XLOC
 JSR SUBROUT 
 LDX #$11 
 STX SHAPE
 LDX #$20 
 STX XLOC
 JSR SUBROUT 
 LDA #$80
 JSR WAIT
 LDX #$16 
 STX SHAPE
 LDX #$1D 
 STX XLOC
 JSR CHARGEN
 LDX #$14 
 STX SHAPE
 LDX #$1D 
 STX XLOC
 JSR SUBROUT 
 LDX #$15 
 STX SHAPE
 LDX #$20 
 STX XLOC
 JSR SUBROUT 
 LDA #$80
 JSR WAIT
 RTS
SUBROUT LDA YLOC 
 STA SAFEY
 SEC
 SBC #$12
 STA YLOC
 JSR CHARGEN
 LDA SAFEY
 STA YLOC
 RTS
CLRSCRN LDA #$00
 STA COUNTER
 LDA #$08
 STA PITCH
 LDA #$0A
 STA FLAGBYTE
LINELOOP LDY COUNTER
 LDA (TABLO),Y 
 STA SCLO
 LDA (TABHI),Y
 STA SCHI
 LDA #$00
 TAY
 LDX #$28
BYTELOOP STA (SCLO),Y 
 INC SCLO
 DEX
 BNE BYTELOOP
 JSR INTERP ;SOUND 
 INC COUNTER
 LDA COUNTER
 CMP #$C0
 BNE LINELOOP
*************************** 
*ROUTINE TO "RING UP" BONUS 
***************************
*
*
 LDA #$02
 STA XLOC
 LDA #$56
 STA YLOC
 LDA #$21
 STA SHAPE
BONLOOP JSR CHARGEN
 LDA SHAPE 
 CMP #$25 
 BEQ PRINT$ 
 INC XLOC
 INC XLOC
 INC XLOC
 INC SHAPE 
 JMP BONLOOP
*
*
PRINT$ LDA #$14
 STA XLOC
 LDA #$30
 STA SHAPE
 JSR CHARGEN
 LDA #$26
 STA SHAPE
 LDA #$17
 STA XLOC
ZEROLOOP JSR CHARGEN
 INC XLOC
 INC XLOC
 INC XLOC
 LDA XLOC
 CMP #$26
 BNE ZEROLOOP
 LDA MENSPENT
 CMP #$00
 BNE MENLOST 
 LDA #$64 
 STA COUNT
 JMP RINGUP
MENLOST CMP #$01
 BNE MENLOST2
 LDA #$4C 
 STA COUNT
 JMP RINGUP
MENLOST2 CMP #$02
 BNE MENLOST3
 LDA #$33 
 STA COUNT
 JMP RINGUP
MENLOST3 CMP #$03 
 BNE NOBONUS
 LDA #$1A 
 STA COUNT
 JMP RINGUP
*
*
NOBONUS BRK
*
*
RINGUP LDA #$00
 STA TENS
 STA THOUS
 LDA #$0B
 STA FLAGBYTE
 LDA #$FF 
 STA COUNTER
RINGLOOP SED
 LDA THOUS
 CLC
 ADC #$01
 STA THOUS
 BCS TENTHOU
 DEC COUNT
 BEQ DONERING
 DEC COUNTER
 DEC COUNTER
 DEC COUNTER
 BNE NOSPEED
 INC COUNTER
 INC COUNTER
 INC COUNTER
NOSPEED LDA COUNTER
 JSR PAUSE
 JSR INTERP ;SOUND
 JSR PRINTNUM
 JMP RINGLOOP 
TENTHOU CLD
 LDA #$27
 STA SHAPE
 LDA #$17
 STA XLOC
 JSR CHARGEN
 JSR PRINTNUM
*
*
DONERING BRK
*
*
PRINTNUM CLD
 LDA #$26 
 STA SHAPE
 LDA #$23
 STA XLOC
 JSR CHARGEN
 LDA #$20
 STA XLOC
 JSR CHARGEN
 LDA THOUS
 AND #$0F 
 CLC 
 ADC #$26
 STA SHAPE
 LDA #$1D
 STA XLOC
 JSR CHARGEN
 LDA THOUS
 LSR A
 LSR A
 LSR A
 LSR A
 CLC
 ADC #$26
 STA SHAPE
 LDA #$1A
 STA XLOC
 JSR CHARGEN
 RTS
*
*
******************************
* PROGRAM TO PRODUCE ASCENDING
* AND DESCENDING TONES FOR 
* JUMPING MAN.  CALL SEVERAL
* TIMES WITH FLAGBYTE=01 FOR
* JUMP DOWN AND FLAGBYTE=02
* FOR JUMPING UP
******************************
INTERP LDA FLAGBYTE
 BEQ BEGIN 
 CMP #$01
 BEQ DESCEND
 CMP #$02
 BEQ ASCEND
 CMP #$07
 BEQ CHOPPER
 CMP #$05
 BEQ DIE
 CMP #$03 
 BEQ BELL
 CMP #$0A
 BEQ CLRSOUND
 CMP #$0B
 BEQ BONSOUND
 CMP #$08 
 BEQ SIREN
 CMP #$09
 BEQ SIREN1
 RTS
*
*
CLRSOUND JMP CLRSOUND1
*
*
BEGIN JMP BEGIN1
*
*
BONSOUND JMP BSOUND
*
*
CHOPPER LDA SPKR 
 RTS 
*
*
BELL LDA #$20 
 STA PITCH
 LDA #$20 
 STA DURATION1
 JSR TONE
 LDA #$50 
 JSR PAUSE
 LDA #$20 
 STA DURATION1
 JSR TONE
 LDA #$50 
 JSR PAUSE 
 RTS
*
*
DIE LDA #$10
 STA DURATION1
 JSR TONE
 INC PITCH
 RTS
*
*
DESCEND LDA #$01
 STA ADJUST
 LDA #$07 
 STA DURATION
 LDA #$08 
 STA COUNTDOWN
 JSR OUTPUT
 RTS
*
*
ASCEND LDA #$FF
 STA ADJUST
 LDA #$07 
 STA DURATION
 LDA #$08 
 STA COUNTDOWN
 JSR OUTPUT
 RTS
*
*
SIREN LDA #$60 
 STA PITCH
 LDA #$20 
 STA DURATION1
 JSR TONE
 RTS
SIREN1 LDA #$80 
 STA PITCH
 LDA #$20 
 STA DURATION1
 JSR TONE
 RTS
*
*
CLRSOUND1 LDA #$04 
 STA DURATION1
 JSR TONE
 INC PITCH
 RTS
*
*
BSOUND LDA #$10
 STA PITCH
 LDA #$01 
 STA DURATION1
 JSR TONE
 RTS
*
*
OUTPUT LDX COUNTDOWN
 STX DURATION1 
 JSR TONE 
 LDX COUNTDOWN
 LDA PITCH
 CLC
 ADC ADJUST
 STA PITCH
 DEC DURATION
 BNE OUTPUT 
 RTS
****************************
* PROGRAM TO PRODUCE 
* TICK-TOCK SOUND FOR RUNNING
* MAN. DESTROYS ACCUM
****************************
*
*
*
*
BEGIN1 LDA SAFECOUNT 
 STA COUNTDOWN 
*
*
TIC LDA $C030
 LDA TESTBYTE
 JSR PAUSE
 DEC COUNTDOWN 
 BNE TIC 
 LDA TESTBYTE
 EOR #$03 
 STA TESTBYTE
 RTS
************************
* PROGRAM TO PRODUCE   *
* TONES OF VARIABLE    *
* PITCH AND DURATION   *
* WITH CONSTANT TIMING *
************************
*
*
TONE LDX PITCH
TONE1 LDA PITCH
 CMP #$01
 BEQ TONE2
 LDA SPKR
TONE2 DEY
 BNE NUPITCH
 DEC DURATION1 
 BEQ TERMINATE 
NUPITCH DEX
 BNE TONE2 
 LDX PITCH
 JMP TONE1 
TERMINATE RTS 
*
*
GAMEOVER1 LDA #$0A
 STA FLAGBYTE
 LDA #$6E
 STA PITCH
 LDA #$2F 
 STA YLOC
SCROLLOOP LDA #$BD 
 STA SAFEY
 INC YLOC
 LDA YLOC
 CMP #$BD
 BEQ DONESCROLL
 JSR INTERP ;SOUND
LINESCROLL LDY SAFEY
 LDA (TABHI),Y
 STA SCHI
 LDA (TABLO),Y
 STA SCLO
 DEY
 CPY YLOC 
 BEQ SCROLLOOP
 LDA (TABLO),Y
 STA SHLO
 LDA (TABHI),Y
 STA SHHI
 STY SAFEY
 LDY #$00
BYTESCROLL LDA (SHLO),Y
 STA (SCLO),Y
 INY
 CPY #$28
 BNE BYTESCROLL
 JMP LINESCROLL
DONESCROLL LDA #$28 
 STA SAFEX 
CARLOOP LDA #$08 
 STA FLAGBYTE
 LDA #$38
 STA SHAPE
 JSR DRAWCAR
 LDA #$4D
 JSR DRAWGAME 
 JSR INTERP
*
 LDA #$31
 STA SHAPE
 JSR DRAWCAR
 LDA #$19
 JSR DRAWGAME
 LDA #$20 
 JSR PAUSE 
*
 LDA SAFEX 
 CLC
 ADC #$09 
 STA XLOC
 LDA #$AC 
 STA YLOC
 LDA #$00
 STA SHAPE
 JSR CHARGEN
*
 DEC SAFEX
 LDA #$00
 STA SHAPE
 LDA SAFEX
 CLC 
 ADC #$0C 
 STA XLOC
 LDA #$9A 
 STA YLOC
 JSR CHARGEN
*
 LDA #$09 
 STA FLAGBYTE
 LDA #$46
 STA SHAPE
 JSR DRAWCAR
 LDA #$4D
 JSR DRAWGAME
 JSR INTERP
*
 LDA #$3F
 STA SHAPE
 JSR DRAWCAR
 LDA #$19
 JSR DRAWGAME
 LDA #$20 
 JSR PAUSE 
*
 LDA SAFEX 
 CLC
 ADC #$09 
 STA XLOC
 LDA #$AC 
 STA YLOC
 LDA #$00
 STA SHAPE
 JSR CHARGEN
*
 LDA SAFEX
 CLC 
 ADC #$0B 
 STA XLOC
 LDA #$9A 
 STA YLOC
 JSR CHARGEN
 DEC SAFEX
 LDA SAFEX
 CMP #$F2 
 BEQ BREAK 
 JMP CARLOOP
BREAK BRK
DRAWGAME STA SHAPE    
 LDA SAFEX
 CLC
 ADC #$04 
 BMI SKIPGAME
 CLC
 ADC #$08 
 STA XLOC
 LDA #$AC 
 STA YLOC
 LDA #$04 
 STA COUNTER
GAMELOOP JSR CHARGEN
 INC SHAPE 
 INC XLOC
 INC XLOC
 INC XLOC
 DEC COUNTER
 BNE GAMELOOP
 LDA SHAPE
 STA SAVA 
 LDA #$00
 STA SHAPE
 JSR CHARGEN
 LDA SAVA
 STA SHAPE
 INC XLOC
 INC XLOC
 INC XLOC
 LDA #$04 
 STA COUNTER
OVERLOOP JSR CHARGEN
 INC SHAPE 
 INC XLOC
 INC XLOC
 INC XLOC
 DEC COUNTER
 BNE OVERLOOP
 LDA #$00
 STA SHAPE
 JSR CHARGEN
 RTS
SKIPGAME LDA #$68 
 JSR PAUSE
 RTS
DRAWCAR LDA SAFEX 
 CLC 
 ADC #$03
 STA XLOC
 LDA #$03
 STA COUNTER
 LDA #$9A 
 STA YLOC
TOPLOOP JSR CHARGEN
 INC SHAPE
 INC XLOC
 INC XLOC
 INC XLOC
 DEC COUNTER
 BNE TOPLOOP
 LDA SAFEX
 STA XLOC
 LDA #$04
 STA COUNTER
 LDA #$AC 
 STA YLOC
BOTMLOOP JSR CHARGEN
 INC XLOC
 INC XLOC
 INC XLOC 
 INC SHAPE
 DEC COUNTER
 BNE BOTMLOOP
 RTS
*
*
GAMEOVER JMP GAMEOVER1 
*
*
RESTORE INC MENSPENT 
 DEC THIEVES 
 BEQ GAMEOVER
 LDA PAGE
 BEQ RCONT 
 CMP #$02
 BNE NOFLIP 
 LDA SECPAGE
NOFLIP LDA LOAD+2
 STA DEST3+2
 STA DEST4+2
 LDA LOAD+1
 STA DEST3+1
 STA DEST4+1
 LDA DEST+2
 STA LOAD3+2
 STA LOAD4+2
 LDA DEST+1
 STA LOAD3+1 
 STA LOAD4+1
CHECK2 LDA LOAD3+1
 CMP #$00 
 BNE LOAD3
 LDA LOAD3+2
 CMP #$20
 BEQ RCONT 
LOAD3 LDA $FFFF 
 TAY
DEST4 LDA $FFFF 
LOAD4 STA $FFFF 
 TYA
DEST3 STA $FFFF 
 LDA LOAD3+1
 SEC
 SBC #$01
 STA LOAD3+1
 STA LOAD4+1
 BCS INCSAVE3
 DEC LOAD3+2
 DEC LOAD4+2
INCSAVE3 LDA DEST3+1
 SEC
 SBC #$01
 STA DEST3+1
 STA DEST4+1
 BCS INCSAVE4
 DEC DEST3+2
 DEC DEST4+2
INCSAVE4 JMP CHECK2
RCONT LDA PRIPAGE
 LDY JUMPPOINTER
 LDA JUMPTABLE,Y
 SEC
 SBC #$03
 STA JUMPTABLE,Y
 LDA #$01
 STA XLOC
 LDA #$20
 STA YLOC 
 LDA #$01
 STA JUMPPOINTER
 LDA #$00
 STA PAGE
 LDA #$5A 
 STA FINISH
 LDA #$00 
 STA DEST+1
 STA LOAD1+1
 STA DEST1+1 
 LDA #$20
 STA DEST+2
 STA DEST1+2
 LDA #$60
 STA LOAD1+2
 LDA #$BE 
 STA BOTTOM+1
 STA CLRBYTE
 JMP STARTUP
*************************
*
* PAUSE ROUTINE A REG 
* DETERMINES LENGTH MAX
* OF HEX FF EQUALS .16 SEC
*
*************************
*
*
PAUSE SEC
PAUSE2 PHA
PAUSE3 SBC #$01
 BNE PAUSE3
 PLA
 SBC #$01
 BNE PAUSE2
 RTS
*************************
*
* PROGRAM TO GENERATE
* 18 X 21 SHAPES ON HIRES   
* SCREEN. USES LOOKUP TABLE
*
*************************
*
*
CHARGEN LDA XLOC
 CMP #$FE
 BCS PRNTCHAR
 CMP #$28
 BCS NOPRINT
PRNTCHAR LDA #$00 
 STA CPOINT
 LDA SHAPE 
 ASL A 
 TAX
 LDA LTHI,X 
 STA SHLO
 LDA LTHI+1,X 
 STA SHHI
 LDA #$12 
 STA YCOUNT ;18 LINE SHAPE
*
* SET UP ADRESS OF BYTE
LOOP3 LDY YLOC
BOTTOM CPY #190 
 BCS NOLINE
 LDA (TABLO),Y  
 STA SCLO ;OF LINE
 LDA (TABHI),Y
 STA SCHI ;HI BYTE
 LDY CPOINT 
 LDA (SHLO),Y ;GET SHAPE BYTE
 INC CPOINT
 LDY XLOC 
 CPY #$27 
 BCS SKIPBYTE
 STA (SCLO),Y
SKIPBYTE LDY CPOINT
 LDA (SHLO),Y
 INC CPOINT
 LDY XLOC 
 INY
 CPY #$27 
 BCS NEXTBYTE
 STA (SCLO),Y
NEXTBYTE LDY CPOINT
 LDA (SHLO),Y
 INC CPOINT
 LDY XLOC
 INY
 INY
 CPY #$27 
 BCS NOLINE2 
 STA (SCLO),Y
NOLINE2 INC YLOC
 DEC YCOUNT ;DONE 1 LINE
 BNE LOOP3 ;DO IT AGAIN
 LDA YLOC
 SEC
 SBC #$12
 STA YLOC
 RTS
NOPRINT LDA #$1A 
 JSR PAUSE
 RTS
NOLINE INC CPOINT
 INC CPOINT
 INC CPOINT 
 JMP NOLINE2
LTHI DW 32768 
 DW 32822
 DW 32876
 DW 32930
 DW 32984
 DW 33038
 DW 33092
 DW 33146
 DW 33200
 DW 33254
 DW 33308
 DW 33362
 DW 33416
 DW 33470
 DW 33524
 DW 33578
 DW 33632
 DW 33686
 DW 33740
 DW 33794
 DW 33848
 DW 33902
 DW 33956
 DW 34010
 DW 34064
 DW 34118
 DW 34172
 DW 34226
 DW 34280
 DW 34334
 DW 34442   
 DW 34388 
 DW 34496
 DW 34550
 DW 34604
 DW 34658
 DW 34712
 DW 34766
 DW 34820
 DW 34874
 DW 34928
 DW 34982
 DW 35036
 DW 35090
 DW 35144
 DW 35198
 DW 35252
 DW 35306
 DW 35360
 DW 35414
 DW 35468
 DW 35522
 DW 35576
 DW 35630
 DW 35684
 DW 35738
 DW 35792
 DW 35846
 DW 35900
 DW 35954
 DW 36008
 DW 36062
 DW 36116
 DW 36170
 DW 36224
 DW 36278
 DW 36332
 DW 36386
 DW 36440
 DW 36494
 DW 36548
 DW 36602
 DW 36656
 DW 36710
 DW 36764
 DW 36818
 DW 36872
 DW 36926
 DW 36980
 DW 37034
 DW 37088
 DW 37142
 DW 37196
 DW 37250
 DW 37304
 DW 37358
 DW 37412
 DW 37466
 DW 37520
 DW 37574
TWINKLE DW $2012 
 DW $2498
 DW $298F
 DW $2227
 DW $2115 
 DW $309F 
 DW $208D 
 DW $359A
 DW $3690
 DW $319F 
TWINKPOINT DFB 00
WAITVAL DFB 01
SAFEY DFB 32
SAFEX DFB $00
COUNT DFB 01
SHAPE2 DFB 01
SAVA DFB 01
DEATHSHAPE DFB 00
DEATHCOUNTER DFB 00
MENCOUNT DFB $00
MENSPENT DFB $00
JUMPPOINTER DFB 01
PAGE DFB 00 
COUNTER DFB 01
FINISH DFB $5A 
NOTE DFB 00
PLAYON DFB 00
JUMPTABLE DFB 06
 DFB 00
 DFB 19
 DFB 01
 DFB 32
 DFB 00
 DFB 05
 DFB 01
 DFB 18
 DFB 01
 DFB 31
 DFB 00
 DFB 04
 DFB 01
 DFB 17
 DFB 00
NOTETABLE DW $E085 
 DW $C085 
 DW $5085 
 DW $C085 
 DW $0201 
 DW $A877 
 DW $0201  
 DW $407E 
 DW $907E 
 DW $0201
 DW $4085 
 DW $9085 
 DW $0201
 DW $408C 
 DW $9085 
PITCH DFB $00 
DURATION DFB $07 
ADJUST DFB $01 
FLAGBYTE DFB $00
DELAYBYTE DFB $00
COUNTDOWN DFB $08 
SAFECOUNT DFB $10
TESTBYTE DFB $10
DURATION1 DFB $01
YLOC DFB $20
XLOC DFB $00
SHAPE DFB $00
CPOINT DFB $00
YCOUNT DFB $00
THOUS DFB $00
TENS DFB $00
RUNSPEED DFB $30
SPEEDCOUNT DFB $00
----------------------------------

Textfile HEIST III
